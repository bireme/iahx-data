/*
    Generation of input XML based on LILACS fields
 */

 if p(v2) then '  <doc>'/ fi				/* Document opening */

   |    <field name="id">prfx-|v2|</field>|/		/* Document Unic Identifier */
 ( |    <field name="db">|v4|</field>|/ )		/* Data Base names */
   |    <field name="cc">|v1|</field>|/			/* Responsible by document */
 if p(v5) or p(v706) then '    <field name="type">',@resources/doctype.pft,'</field>'/ fi
 /* Full text access */
 if p(v8) then
 	if v8^u>'' then '    <field name="fulltext">1</field>'/ fi
  	(
 		if p(v8^u) then
  			|    <field name="ur">|v8^u|</field>|/
  		else
  			|    <field name="ur">|v8^i|</field>|/
   		fi
  	)
 fi
 
 /* Authors, titles and pagination (analitics) */
 if p(v10) or p(v11) then
 	( if p(v10) then |    <field name="au">|v10^*|</field>|/ fi )
 	( if p(v11) then |    <field name="au">|v11^*|</field>|/ fi )
 	if p(v12) then
 		(
 			if p(v12^i) then
 				|    <field name="ti_|v12^i|">|v12^*|</field>|/
 			else
 				|    <field name="ti">|v12^*|</field>|/
 			fi
 		)
 	fi
 	|    <field name="ti">|v13^*|</field>|/
 	if p(v14) then
 		(
 			if p(v14^f) and p(v14^l) then
 				|    <field name="pg">|v14^f| - |v14^l|</field>|/
 			else
 				|    <field name="pg">|v14^*|</field>|/
 			fi
 		)
 	fi
 fi
 
 /* Periodic articles */
 if v06 = 'as' then
 	if p(v30) then
 		'    <field name="fo">'
 		v30|;|
 		v31
 		|(|v32|)|
 		|: |v14^f|-|v14^l|, |
 		v64|. |
 		v38+|, |,".",n13
 	'</field>'/ fi
 fi
 
 /* Monographics Chapters */
 if v06 : 'am' then
 	if p(v16) or p(v17) then
 		if p(v16) then '    <field name="fo">In. '(v16^*+|; |".") fi
 		if a(v16) then '    <field name="fo">In. '(v17^*+|; |".") fi
 		(" "v18^*+| / |". ")
 		v66', '
 		(v62+|;|)', '
 		v64'. '
 		|p.|v14^*,|-|v14^l
 		|, |v38'. '
 		|(|v30
 		|, |v31
 		|, |v32
 		if p(v30) then ').' fi
 		| (|v68|)|
 		if p(v68) then '.' fi
 		'</field>'/
 	fi
 fi
 
 /* Monographics */
 if v06*0.1 = 'm' then
 	if p(v16) or p(v17) then
 		( if p(v16) then |    <field name="au">|v16^*|</field>|/ fi )
 		( if p(v17) then |    <field name="au">|v17^*|</field>|/ fi )
 		(
 			if p(v18) then
 				if p(v18^i) then
 					|    <field name="ti_|v18^i|">|v18^*|</field>|/
 				else
 					|    <field name="ti">|v18^*|</field>|/
 				fi
 			fi
 		)
 		|    <field name="ti">|v19^*|</field>|/
 		|    <field name="pg">|v20^*|</field>|/
 		|    <field name="vi">|v21^*|</field>|/
 	fi
 	if p(v66) then
 		'    <field name="fo">'
 		v66,'; '
 		(v62+|;|),'; '
 		v64,'. '
 		v20| p. |
 		v38+|, |"."
 		|(|v30
 		|, |v31
 		|, |v32
 		if p(v30) then ').' fi
 		| (|v68|).|
 		'</field>'/
 	fi
 	if p(v30) then
 		'    <field name="fo">'
 		|(|v30
 		|, |v31
 		|, |v32
 		').'|(|v68|).|
 		'</field>'/
 	fi
 fi
 
 /* Collection */
 if v06 = 'c' then
 	if p(v23) or p(v24) then
 		( if p(v23) then |    <field name="au">|v23^*|</field>|/ fi )
 		( if p(v24) then |    <field name="au">|v24^*|</field>|/ fi )
 		(
 			if p(v25) then
 				if p(v25^i) then
 					|    <field name="ti_|v25^i|">|v25^*|</field>|/
 				else
 					|    <field name="ti">|v25^*|</field>|/
 				fi
 			fi
 		)
 		|    <field name="ti">|v26^*|</field>|/
 		|    <field name="ntv">|v27^*|</field>|/
 	fi
 	if p(v66) then
 		'    <field name="fo">'
 		v66'; '
 		(v62+|;|)'; '
 		v64'. '
 		v20| p. |
 		v38'.'
 		'</field>'/
 	fi
 fi
 
 /* */
 /* Title, ISSN, Volume, Number  */
 if p(v30) then
 	( |    <field name="ta">|v30^*|</field>|/ )
 	( |    <field name="is">|v35^*|</field>|/ )
 	( |    <field name="vi">|v31^*|</field>|/ )
 	( |    <field name="ip">|v32^*|</field>|/ )
 fi
 /* Language */
 (
 	if p(v40) then
 		|    <field name="la">|v40^*|</field>|/
 	else if p(v12^i) then
 		|    <field name="la">|v12^i|</field>|/
 	else if p(v18^i) then
 		|    <field name="la">|v18^i|</field>|/
 	fi fi fi
 )

 /* Thesis */
 ( |    <field name="th_le">|v49^*|</field>|/ )
 |    <field name="th_in">|v50^*|</field>|/
 |    <field name="th_ti">|v51^*|</field>|/

 /* Publisher, Edition, Date, City, Country, ISBN */
 if p(v62) or p(v64) then
       ( |    <field name="pu">|v62^*|</field>|/ )
 	 |    <field name="ed">|v63^*|</field>|/
 	 |    <field name="dp">|v64^*|</field>|/
 	 |    <field name="da">|v65^*|</field>|/
 	 |    <field name="cy">|v66^*|</field>|/
 	 |    <field name="cp">|v67^*|</field>|/
 	 |    <field name="isbn">|v69^*|</field>|/
 fi
 
 /* Abstract */
 (
 	if p(v83) then
 		if p(v83^i) then
 			|    <field name="ab_|v83^i|">|v83^*|</field>|/
 		else if p(v41) then
 			|    <field name="ab_|v41[1]|">|v83^*|</field>|/
 		else
 			|    <field name="ab">|v83^*|</field>|/
 		fi fi
 	fi
 )
 
 /* Congress */
 ( |    <field name="cn_in">|v52^*|</field>|/ )
 ( |    <field name="cn_na">|v53^*|</field>|/ )
 ( |    <field name="cn_dt">|v54^*|</field>|/ )
 ( |    <field name="cn_da">|v55^*|</field>|/ )
 ( |    <field name="cn_cy">|v56^*|</field>|/ )
 ( |    <field name="cn_co">|v57^*|</field>|/ )

 /* Projects */
 ( |    <field name="pr_in">|v58^*|</field>|/ )
 ( |    <field name="pr_na">|v59^*|</field>|/ )
 ( |    <field name="pr_nu">|v60^*|</field>|/ )

 /* Publication type */
 ( |    <field name="pt">|v71^*|</field>|/ )

 /* Limits and Descriptors */
 ( |    <field name="mh">|v76^*|</field>|/ )
 ( |    <field name="mh">|v87^*|</field>|/ )
 ( |    <field name="mh">|v88^*|</field>|/ )

 if p(v2) then '  </doc>'/ fi	/* Document closure */

